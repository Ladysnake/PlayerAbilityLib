import net.fabricmc.loom.task.RemapJarTask

plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'io.github.ladysnake.chenille' version '0.13.0'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

chenille {
    configurePublishing {
        mainArtifact = remapJar.archiveFile
        withLadysnakeMaven()
        withCurseforgeRelease()
        withGithubRelease()
        withModrinthRelease()
    }

    configureTestmod {
        withBaseTestRuns()
    }

    license = project.license_header
    javaVersion = 21
}

repositories {
    maven {
        name = "Ladysnake Mods"
        url = "https://maven.ladysnake.org/releases"
        content {
            includeGroup("io.github.ladysnake")
            includeGroup("org.ladysnake")
            includeGroupByRegex("dev\\.emi.*")
            includeGroupByRegex("dev\\.onyxstudios.*")
        }
    }

}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modLocalImplementation "org.ladysnake:elmendorf:${elmendorf_version}"
    api "org.jetbrains:annotations:19.0.0"
}

java {
    withSourcesJar()
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.register("testmodJar", Jar) {
    archiveBaseName.set('PalTest')
    archiveClassifier.set('dev')
    from(sourceSets.testmod.output) {
        include 'fabric.mod.json'
        expand 'version': project.version
    }
    from(sourceSets.testmod.output) {
        exclude 'fabric.mod.json'
    }
    dependsOn testmodClasses
}

tasks.register("remapTestmodJar", RemapJarTask) {
    archiveBaseName.set('PalTest')
    input = testmodJar.archiveFile
    addNestedDependencies = false
    dependsOn project.testmodJar
}

build.dependsOn(remapTestmodJar)
